Script = <Newline>* TitlePage? Scene (<Newline>+ Scene)* <Newline>*
TitlePage = TitlePagePart (<Newline> TitlePagePart)*
<TitlePagePart> = Title | Credit | Author | Source | DraftDate | Contact | OtherPart
Title = <'Title:'> (Line | Multiline)
Credit = <'Credit:'> Line | Multiline
Author = <'Author'> <'s'>? <':'> Line | Multiline
Source = <'Source:'> Line | Multiline
Contact = <'Contact:'> Line | Multiline
DraftDate = <'Draft date:'> Line | Multiline
OtherPart = #'(\w|[ \t])+' <':'> Line | Multiline
Multiline = (<Newline> <WS>+ #".+")+
Scene = Heading <Newline>+ <WS> Part (<Newline>+ Part)*
Heading = ('INT.' | 'EXT.' | <'.'>) Line
<Part> = Dialogue / Transition / Action
Newline = '\n'
Dialogue = Character <Newline> (Parenthetical <Newline>)? Talk (<Newline> Parenthetical <Newline> Talk)*
Character = <WS> #'[A-Z\- ]+'
Talk = Line (<Newline> Line)*
Parenthetical = <WS> <'('> Line (<Newline> Line)* <')'>
Action = Line (<Newline> Line)*
Transition = <WS> #'[A-Z ]+TO:'
WS = #'[\ \t]*'
Line = <WS> !'(' #'.'+
